theory SecureDrop
begin

builtins: symmetric-encryption, asymmetric-encryption, diffie-hellman, signing
functions: hkdf/1

rule Register_pk:
  [ Fr(~ltk) ]
  -->
  [ !Ltk($A, ~ltk), !Pk($A, pk(~ltk)), Out(pk(~ltk))]

rule Reveal_ltk:
  [ !Ltk(A, ltk) ]
  --[ Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Register_dh_pk:
  let public_component = 'g'^~ltk in
  [ Fr(~ltk) ]
  -->
  [ !Ltk_dh($A, ~ltk), !Pk_dh($A, public_component), Out(public_component)]

rule Reveal_dh_ltk:
  [ !Ltk_dh(A, ltk) ]
  --[ Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Journalist_Init:
  let je_pk = pk(~je_sk)
      sig_je_pk = sign(<'ephemeral', je_pk>, j_sk)
      sig_jc_pk = sign(<'fetch', jc_pk>, j_sk)
  in
  [ !Ltk($J, j_sk)
  , !Pk_dh($J, jc_pk)
  , Fr(~je_sk)
  ]
  -->
  [ Out(<je_pk, sig_je_pk, jc_pk, sig_jc_pk>)
  , Journalist_Ephemeral_Key($J, ~je_sk)
  , Journalist_Init($J, jc_pk)
  ]

rule Source_Submit:
  let k = hkdf(~ss)
      c = senc(<~m, s_pk>, k)
      me_pk = 'g'^~me_sk
      m_gdh = jc_pk^~me_sk 
      ss_encap = aenc(~ss, je_pk)
  in
  [ !Pk($J, j_pk)
  , !Pk_dh($S, s_pk)
  , Journalist_Init($J, jc_pk)
  , In(<je_pk, sig_je_pk, jc_pk, sig_jc_pk>)
  , Fr(~m)
  , Fr(~ss)
  , Fr(~me_sk) ]
  --[ Eq(verify(sig_je_pk, <'ephemeral', je_pk>, j_pk), true)
    , Eq(verify(sig_jc_pk, <'fetch', jc_pk>, j_pk), true)
    , Source_Submission($S, $J, ~m)
    ]->
  [ Out(<c, m_gdh, me_pk, ss_encap>) ]

rule Journalist_Recv:
  let ss = adec(ss_encap, je_sk)
      k = hkdf(ss)
      msg = sdec(c, k)
      m = fst(msg)
  in
  [ In(<c, me_pk, ss_encap>)
  , Journalist_Ephemeral_Key($J, je_sk) ]
  --[ Journalist_Read($J, m) ]->
  []

restriction Eq:
  "All t1 t2 #x. Eq(t1, t2) @ #x ==> t1 = t2"

lemma Executability:
  exists-trace
  "Ex #t1 #t2 s j m. 
      Source_Submission(s, j, m) @ #t1
    & Journalist_Read(j, m) @ #t2
    & (not Ex #x. SomeCompromise() @ #x)"

lemma Confidentiality:
  all-traces
  "All #t1 s j m. Source_Submission(s, j, m) @ #t1
    ==> (not Ex #x. K(m) @ #x) | (Ex #x. SomeCompromise() @ #x)"

end